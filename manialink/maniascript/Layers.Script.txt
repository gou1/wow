#Include "Lib.Script.txt" as Lib

Text Private_Layers_DogeItem(Text Id, Text Name, Text ImageUrl, Vec3 Position, Vec2 Size)
{
    return """
    <frame posn="{{{Position.X}}} {{{Position.Y}}}">
        <quad id="doge-details" data-id="{{{Id}}}" scriptevents="1" posn="0 0 0" sizen="{{{Size.X}}} {{{Size.Y}}}" image="{{{ImageUrl}}}"/>
        <label posn="{{{Size.X/2.}}} {{{-Size.Y+5}}} 1" sizen="50 10" text="{{{Name}}}" style="TextTitle2Blink" halign="center" valign="center2"/>
    </frame>
    """;
}

Text Layers_Loading() {
    return """
    <video data="http://webmbassy.com/v/dog-doge-shaking-Shiba-Inu-wow-1396293701t.webm" sizen="180 180" valign="center" halign="center" play="1" looping="1" />
    <label posn="0 0 1" sizen="291 95" text="Loading" style="TextTitle2Blink" textsize="20" halign="center" valign="center"/>
    """;
}

Text Layers_Success() {
    return """
    <video data="http://webmbassy.com/v/doge-batman-plane-thumbs-up-dogecoin-1391878824C.webm" sizen="320 180" valign="center" halign="center" play="1" looping="0" />
    """;
}

Text Layers_OneDoge(Text Id, Text Name, Text ImageUrl, Vec3 Position)
{
    return Private_Layers_DogeItem(Id, Name, ImageUrl, Position, <150., 150.>)^"""
    <script>
    main()
    {
        while(True)
        {
            foreach(Event in PendingEvents)
            {
                if (Event.Type == CMlEvent::Type::MouseClick)
                {
                    SendCustomEvent("Doge.Section.Home", Text[]);
                }
            }
            yield;
        }
    }
    </script>
    """;
}

Text Layers_Doges(Text[Text][] Data) {
    declare Text Output;
    declare Vec3 Position = <-150., 80., 0.>;

    Output ^= Private_Layers_DogeItem("0", "","http://placehold.it/300/09f/fff.png&text=Born%20a%20Doge#.png", Position, <50., 50.>);

    Position.X += 55;

    foreach(DogeItem in Data) {
        Output ^= Private_Layers_DogeItem(DogeItem["id"], DogeItem["name"], DogeItem["image_url"], Position, <50., 50.>);
        Position.X += 55;
        if(Position.X > 100.)
        {
            Position.X = -150.;
            Position.Y += -55.;
        }
    }

    Output ^= """
    <script>
     main()
    {
        while(True)
        {
            foreach(Event in PendingEvents)
            {
                if (Event.Type == CMlEvent::Type::MouseClick)
                {
                    if(Event.Control.DataAttributeGet("id") == "0") {
                        SendCustomEvent("Doge.Create", Text[]);
                    } else {
                        SendCustomEvent("Click", [Event.ControlId, Event.Control.DataAttributeGet("id")]);
                    }
                }
            }
            yield;
        }
    }
    </script>
    """;

    return Output;
}